/**
 * Graphic screen library.
 */
class Screen {
	static int width, height, sections, sectionsPerY;
	static boolean color;
	static int mapAddr;
	
	static Array partlyFilled; // Holds bitmasks that have their indices' amount of bits on
	
    /** Initializes the Screen. */
    function void init() {
		var int i;
		let mapAddr = 16384;
		let width = 512;
		let height = 256;
		let sectionsPerY = width/16;
		
		let sections = height * sectionsPerY;
		
		let color = true;
		
		let partlyFilled = Array.new(17);
		
		let i = 1;
		let partlyFilled[0] = 0;
		while (i < 17) {
			let partlyFilled[i] = partlyFilled[i-1] | Math.twoToThe(i-1);
			let i = i + 1;
		}
		return;
    }

    /** Erases the whole screen. */
    function void clearScreen() {
		var int i;
		while (i < sections) {
			do Memory.poke(mapAddr + i, ~color);
			let i = i + 1;
		}
		return;
    }

    /** Sets the color to be used in further draw commands
     *  where white = false, black = true. */
    function void setColor(boolean b) {
		let color = b;
		return;
    }
	
	function void drawSection(int mask, int section) {
		if (color) {
			do Memory.poke(section, Memory.peek(section) | mask);
		} else {
			do Memory.poke(section, Memory.peek(section) & ~mask);
		}
		return;
	}
	
	/** Checks if the given coordinates are inside the screen */ 
	function boolean validCoordinates(int x, int y) {
		return ((~(x < 0) & (x < width)) & (~(y < 0) & (y < height)));
	}
	
	function boolean validX(int x) {
		return (~(x < 0) & (x < width));
	}
	
	function boolean validY(int y) {
		return (~(y < 0) & (y < height));
	}

    /** Draws the (x, y) pixel. */
    function void drawPixel(int x, int y) {
		var int section;
		if (~Screen.validX(x) | ~Screen.validY(y)) {
			do Sys.error(7); // Illegal pixel coordinates
		}
		
		let section = mapAddr + (y * 32) + (x / 16);
		do Screen.drawSection(Math.twoToThe(x & 15), section);
		return;
    }

    /** Draws a line from (x1, y1) to (x2, y2) using Bresenhams line algorithm. */
    function void drawLine(int x1, int y1, int x2, int y2) {
		var int dx, dy; // distances between coordinates' components
		var int error, dError;
		var int remainder, remSum;
		var int absDx;
		
		var int temp;
		
		let dy = y2 - y1;
		if (dy = 0) { // Able to use a specialised function
			do Screen.drawHorizontalLine(x1, x2, y1);
			return;
		}
		
		let dx = x2 - x1;
		if (dx = 0) { // Able to use a specialised function
			do Screen.drawVerticalLine(y1, y2, x1);
			return;
		}
		
		if (~Screen.validCoordinates(x1, y1)) {
			do Sys.error(8); // Illegal line coordinates
		}
		if (~Screen.validCoordinates(x2, y2)) {
			do Sys.error(8); // Illegal line coordinates
		}
		
		// Swap starting points to ease logic
		if (dx < 0) {
			let temp = x1;
			let x1 = x2;
			let x2 = temp;
			let dx = -dx;
			
			let temp = y1;
			let y1 = y2;
			let y2 = temp;
			
			let dy = y2 - y1;
		}
		
		let error = 0;
		let dError = Math.abs(dy/dx);
		let remainder = Math.abs(dy) - (dx*dError);
		let remSum = 0; // cumulative remainders from divisions
		
		while (~(x1 > x2)) {
			do Screen.drawPixel(x1, y1);
			let error = error + dError;
			let remSum = remSum + remainder;
			if (~(remSum < dx)) { // the sum of remainders makes one whole denominator
				let error = error + 1;
				let remSum = remSum - dx;
			}
			while ((error > 0) | ((error = 0) & ~((remSum + remSum) < dx))) {
				do Screen.drawPixel(x1, y1);
				let y1 = y1 + Math.sign(dy);
				let error = error - 1;
			}
			let x1 = x1 + 1;
		}
		return;
    }
	/** Draws a line from (x, y1) to (x, y2) */
	function void drawVerticalLine(int y1, int y2, int x) {
		var int startSection, section, temp;
		
		if (~Screen.validCoordinates(y1, y2)) {
			do Sys.error(8); // Illegal line coordinates
		}
		if (~Screen.validY(y1)) {
			do Sys.error(8); // Illegal line coordinates
		}
		
		if (y2 < y1) {
			let temp = y2;
			let y2 = y1;
			let y1 = temp;
		}
	
		let startSection = mapAddr + (y1*sectionsPerY) + (x/16);
		let section = 0;
		let x = Math.twoToThe(x & 15);
		
		while (~(y1 > y2)) {
			do Screen.drawSection(x, startSection);
			let startSection = startSection + sectionsPerY; // Move one row down
			let y1 = y1 + 1;
		}
		return;
	}
	
	/** Draws a line from (x1, y) to (x2, y) */
	function void drawHorizontalLine(int x1, int x2, int y) {
		var int startAddr, i, tailPixels;
		
		if (~Screen.validCoordinates(x1, y)) {
			do Sys.error(8); // Illegal line coordinates
		}
		if (~Screen.validX(x2)) {
			do Sys.error(8); // Illegal line coordinates
		}
		
		if (x2 < x1) {
			let i = x1;
			let x1 = x2;
			let x2 = i;
		}
		
		let startAddr = mapAddr + (y*sectionsPerY) + (x1/16);
		let tailPixels = (x2 & 15) + 1;
		/** First section */
		let i = x1 & 15;
		if ((16 - i) > (x2 - x1)) { // The whole line fits into the first section
			do Screen.drawSection(~partlyFilled[i] & partlyFilled[tailPixels], startAddr);
			return;
		} else {
			do Screen.drawSection(~partlyFilled[i], startAddr);
			let x1 = x1 + (16 - i);
		}
		/** Midsection */
		let startAddr = startAddr + 1;
		let i = x2 - 16;
		while (~(x1 > i)) {
			do Screen.drawSection(partlyFilled[16], startAddr); // Color the section completely
			let x1 = x1 + 16;
			let startAddr = startAddr + 1;
		}
		/** Last section */
		do Screen.drawSection(partlyFilled[tailPixels], startAddr);
		
		return;
	}

    /** Draws a filled rectangle where the top left corner
     *  is (x1, y1) and the bottom right corner is (x2, y2). */
	function void drawRectangle(int x1, int y1, int x2, int y2) {
		var int temp, curY, baseAddress, curAddress, tailPixels;
		if (~Screen.validCoordinates(x1, y1)) {
			do Sys.error(9); // Illegal rectangle coordinates
		}
		if (~Screen.validCoordinates(x2, y2)) {
			do Sys.error(9); // Illegal rectangle coordinates
		}

		if (y2 < y1) {
			let temp = y1;
			let y1 = y2;
			let y2 = temp;
		}
		if (x2 < x1) {
			let temp = x1;
			let x1 = x2;
			let x2 = temp;
		}
		
		let baseAddress = mapAddr + (y1*sectionsPerY) + (x1/16);
		let curAddress = baseAddress;
		let tailPixels = (x2 & 15) + 1;
		
		let curY = y1;
		
		/** First section */
		let temp = x1 & 15;
		if ((16 - temp) > (x2 - x1)) { // The whole line fits into the first section
			let temp = ~partlyFilled[temp] & partlyFilled[tailPixels]; // first sections pixelmap
			while (~(curY > y2)) {
				do Screen.drawSection(temp, curAddress);
				let curAddress = curAddress + sectionsPerY; // Move one row down
				let curY = curY + 1;
			}
			return;
		}
		
		while (~(curY > y2)) { // The whole line dosesnt fit into the first section
			do Screen.drawSection(~partlyFilled[temp], curAddress);
			let curAddress = curAddress + sectionsPerY;
			let curY = curY + 1;
		}
		let x1 = x1 + (16 - temp);
		let curY = y1;
		
		/** Midsection */
		let baseAddress = baseAddress + 1; // First section is already drawn
		let curAddress = baseAddress;
		let temp = x2 - 16; // Last full section
		while (~(x1 > temp)) {
			while (~(curY > y2)) {
				do Screen.drawSection(partlyFilled[16], curAddress);
				 // Move one row down
				let curAddress = curAddress + sectionsPerY;
				let curY = curY + 1;
			}
			let curY = y1;
			let x1 = x1 + 16;
			let baseAddress = baseAddress + 1;
			let curAddress = baseAddress;
		}
		
		/** Last section */
		while (~(curY > y2)) {
			do Screen.drawSection(partlyFilled[tailPixels], curAddress);
			let curAddress = curAddress + sectionsPerY;
			let curY = curY + 1;
		}
		
		return;
    }

    /** Draws a filled circle of radius r around (cx, cy) using Midpoint circle algorithm variant. */
    function void drawCircle(int cx, int cy, int r) {
		var int y, x, dOver2, yMinusX;
		
		if (r < 0) {
			do Sys.error(13); // Illegal radius
		}
		if (~Screen.validCoordinates(cx-r, cy-r)) {
			do Sys.error(12); // Illegal center coordinates
		}
		if (~Screen.validCoordinates(cx+r, cy+r)) {
			do Sys.error(12); // Illegal center coordinates
		}
		
		let dOver2 = 1 - r;
		let y = 0;
		let x = r;
		while (~(y > x)) {
			do Screen.drawHorizontalLine(x + cx, -x + cx, y + cy); // octants 1 to 4
			do Screen.drawHorizontalLine(y + cx, -y + cx, x + cy); // octants 2 to 3
			do Screen.drawHorizontalLine(-x + cx, x + cx, -y + cy); // octants 5 to 8
			do Screen.drawHorizontalLine(-y + cx, y + cx, -x + cy); // octants 6 to 7
			let y = y + 1;
			if (~(dOver2>0)) { // Change decision criterion for y -> y + 1
				let dOver2 = dOver2 + y + y + 1;
			} else {
				let x = x - 1;  // Change decision criterion for y => y+1
				let yMinusX = y - x;  // Change decision criterion for y => y+1
				let dOver2 = dOver2 + yMinusX + yMinusX + 1;
			}
		}
		return;
    }
}
